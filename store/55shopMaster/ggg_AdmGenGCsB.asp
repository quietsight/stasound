<%@ LANGUAGE="VBSCRIPT" %>
<% 'GGG Add-on ONLY FILE %>
<%
Server.ScriptTimeout = 5400
'This file is part of ProductCart, an ecommerce application developed and sold by NetSource Commerce. ProductCart, its source code, the ProductCart name and logo are property of NetSource Commerce. Copyright 2001-2013. All rights reserved. You are not allowed to use, alter, distribute and/or resell any parts of ProductCart's source code without the written consent of NetSource Commerce. To contact NetSource Commerce, please visit www.productcart.com.
%>
<%PmAdmin="1*2*3*"%><!--#include file="adminv.asp"-->
<!--#include file="../includes/settings.asp"-->
<!--#include file="../includes/storeconstants.asp"--> 
<!--#include file="../includes/stringfunctions.asp"-->
<!--#include file="../includes/adovbs.inc"-->
<!--#include file="../includes/opendb.asp"-->
<!--#include file="../includes/languagesCP.asp" -->
<!--#include file="../includes/SQLFormat.txt"-->
<% pageTitle = "Generate Gift Certificates Codes" %>
<% Section = "products" %>
<%if request("action")<>"gen" then
	response.redirect "ggg_AdmGenGCs.asp"
end if
pcv_IDGC=request("IDGC")
if pcv_IDGC="" or pcv_IDGC="0" then
	response.redirect "ggg_AdmGenGCs.asp"
end if
if Not IsNumeric(pcv_IDGC) then
	response.redirect "ggg_AdmGenGCs.asp"
end if
pcv_GCcount=request("GCcount")
if Not IsNumeric(pcv_GCcount) then
	response.redirect "ggg_AdmGenGCs.asp"
end if

Dim connTemp,query,rs,rsG
call opendb()

Dim Count

'*****************************************
' START - Generate Gift Certificate Codes
'*****************************************

session("adm_generated_gcs")=""

'GCs are generated by Admin
pIdOrder=0
pIdproduct=pcv_IDGC

query="SELECT pcGC.pcGC_Exp, pcGC.pcGC_ExpDate, pcGC.pcGC_ExpDays, pcGC.pcGC_CodeGen, pcGC.pcGC_GenFile, products.sku, products.price from pcGC, Products WHERE pcGC.pcGC_idproduct=" & pcv_IDGC & " AND Products.idproduct=pcGC.pcGC_idproduct AND products.pcprod_GC=1"
set rs=connTemp.execute(query)

if not rs.eof then
	pGCExp=rs("pcGC_Exp")
	pGCExpDate=rs("pcGC_ExpDate")
	pGCExpDay=rs("pcGC_ExpDays")
	pGCGen=rs("pcGC_CodeGen")
	pGCGenFile=rs("pcGC_GenFile")
	pSku=rs("sku")
	pGCAmount=rs("price")
	if pGCGen<>"" then
	else
		pGCGen="0"
	end if
	if (pGCGen=1) and (pGCGenFile="") then
		pGCGen="0"
		pGCGenFile="DefaultGiftCode.asp"
	end if

	if (pGCGen="0") or (not (pGCGenFile<>"")) then
		pGCGenFile="DefaultGiftCode.asp"
	end if
	
	if (pGCExp="2") then
		pGCExpDate=Now()+cint(pGCExpDay)
	end if
	
	if (pGCExp="1") and (year(pGCExpDate)=1900) then
		pGCExp="0"
		pGCExpDate="01/01/1900"
	end if
	
	if (pGCExp="2") and (pGCExpDay="0") then
		pGCExp="0"
		pGCExpDate="01/01/1900"
	end if
	
	if SQL_Format="1" then
		pGCExpDate=(day(pGCExpDate)&"/"&month(pGCExpDate)&"/"&year(pGCExpDate))
	else
		pGCExpDate=(month(pGCExpDate)&"/"&day(pGCExpDate)&"/"&year(pGCExpDate))
	end if
	
	IF (pGCGenFile<>"") THEN
	
			SPath1=Request.ServerVariables("PATH_INFO")
			mycount1=0
			do while mycount1<1
				if mid(SPath1,len(SPath1),1)="/" then
				mycount1=mycount1+1
				end if
				if mycount1<1 then
				SPath1=mid(SPath1,1,len(SPath1)-1)
				end if
			loop
			SPathInfo="http://" & Request.ServerVariables("HTTP_HOST") & SPath1
			if Right(SPathInfo,1)="/" then
				pGCGenFile=SPathInfo & "licenses/" & pGCGenFile					
			else
				pGCGenFile=SPathInfo & "/licenses/" & pGCGenFile
			end if
			L_Action=pGCGenFile
			
		L_postdata=""
		L_postdata=L_postdata&"idorder=" & pIdOrder
		L_postdata=L_postdata&"&orderDate=" & pOrderDate
		L_postdata=L_postdata&"&ProcessDate=" & pProcessDate
		L_postdata=L_postdata&"&idcustomer=" & pIdCustomer
		L_postdata=L_postdata&"&idproduct=" & pIdproduct
		L_postdata=L_postdata&"&quantity=" & pQuantity
		L_postdata=L_postdata&"&sku=" & pSKU
		
		'////////////////////////////////////////////////////////////////////
		'// START:  Create N Codes
		'////////////////////////////////////////////////////////////////////
		session("FailedToConnect") = False
		For k=1 to Cint(pcv_GCcount)


			'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			'// Start: do something
			'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			DO 
			

				If session("FailedToConnect") = False Then
				
					Set srvXmlHttp = server.createobject("Msxml2.serverXmlHttp"&scXML)
					srvXmlHttp.setTimeouts 2500, 2500, 5500, 5500 
					srvXmlHttp.open "POST", L_Action, False
					srvXmlHttp.setRequestHeader "Content-Type", "application/x-www-form-urlencoded"
					srvXmlHttp.send L_postdata				
					if (err.number = -2147012894) then
						session("FailedToConnect") = True
					end if
	
					If srvXmlHttp.status = 200 then
						
						result1 = srvXmlHttp.responseText			
	
						'// Success
						RArray = split(result1,"<br>")
						GiftCode = RArray(2)
						
						'// If have errors from GiftCode Generator
						IF (IsNumeric(RArray(0))=false) and (IsNumeric(RArray(1))=false) then
						
							Tn1=""
							For w = 1 to 6
								Randomize
								myC=Fix(3*Rnd)
								Select Case myC
									Case 0: 
										Randomize
										Tn1=Tn1 & Chr(Fix(26*Rnd)+65)
									Case 1: 
										Randomize
										Tn1=Tn1 & Cstr(Fix(10*Rnd))
									Case 2: 
										Randomize
										Tn1=Tn1 & Chr(Fix(26*Rnd)+97)		
								End Select
							Next						
							GiftCode=Tn1 & Day(Now()) & Minute(Now()) & Second(Now())

						END IF
						
						ReqExist=0
					
						query="SELECT pcGO_IDProduct FROM pcGCOrdered WHERE pcGO_GcCode='" & GiftCode & "'" 
						set rsG=connTemp.execute(query)				
						if not rsG.eof then
							ReqExist=1
						end if
						set rsG = Nothing
						
					else
						
						'// Failure
						ReqExist=0

					End If
					Set srvXmlHttp = Nothing

				Else
				

					Tn1=""
					For w = 1 to 6
						Randomize
						myC=Fix(3*Rnd)
						Select Case myC
							Case 0: 
								Randomize
								Tn1=Tn1 & Chr(Fix(26*Rnd)+65)
							Case 1: 
								Randomize
								Tn1=Tn1 & Cstr(Fix(10*Rnd))
							Case 2: 
								Randomize
								Tn1=Tn1 & Chr(Fix(26*Rnd)+97)		
						End Select
					Next						
					GiftCode=Tn1 & Day(Now()) & Minute(Now()) & Second(Now())

					ReqExist=0
				
					query="SELECT pcGO_IDProduct FROM pcGCOrdered WHERE pcGO_GcCode='" & GiftCode & "'" 
					set rsG=connTemp.execute(query)				
					if not rsG.eof then
						ReqExist=1
					end if
					set rsG = Nothing
				
				End If
				

			LOOP UNTIL ReqExist=0
			'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			'// End: do something
			'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


			'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			'// Start: Insert Gift Codes to Database
			'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			if scDB="Access" then
				query="Insert into pcGCOrdered (pcGO_IdOrder,pcGO_IdProduct,pcGO_GcCode,pcGO_ExpDate,pcGO_Amount,pcGO_Status) values (" & pIdOrder & "," & pIdProduct & ",'" & GiftCode & "',#" & pGCExpDate & "#," & pGCAmount & ",1)"   
			else
				query="Insert into pcGCOrdered (pcGO_IdOrder,pcGO_IdProduct,pcGO_GcCode,pcGO_ExpDate,pcGO_Amount,pcGO_Status) values (" & pIdOrder & "," & pIdProduct & ",'" & GiftCode & "','" & pGCExpDate & "'," & pGCAmount & ",1)"
			end if
			set rsG=connTemp.execute(query)
			set rsG=nothing
			'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			'// End: Insert Gift Codes to Database
			'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			
			session("adm_generated_gcs") = session("adm_generated_gcs") & GiftCode & "***"
			count=count+1
		
		Next
		'////////////////////////////////////////////////////////////////////
		'// END:  Create N Codes
		'////////////////////////////////////////////////////////////////////
		
	END IF

end if
set rs=nothing
session("FailedToConnect") = ""
'*****************************************
' END - Generate Gift Certificate Codes
'*****************************************

call closedb()

%>
<!--#include file="AdminHeader.asp"-->
<table class="pcCPcontent">
<tr>
	<td>
		<div class="pcCPmessageSuccess"><%=count%> gift certificate codes were generated successfully!</div>
	</td>
</tr>
<tr>
	<td class="pcCPspacer"></td>
</tr>
<tr>
	<td>
		<ul>
		  <li><a href="ggg_AdmManageGCs.asp">View/Manage</a> Generated Gift Certificate codes</li>
		  <li><a href="ggg_AdmSendGCs.asp?mode=all">Send</a> Generated Gift Certificate codes to a customer</li>
		</ul>		
	</td>
</tr>
</table>
<!--#include file="AdminFooter.asp"-->